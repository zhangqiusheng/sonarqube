allprojects {
    group = 'com.gridsum.tvdDownload'
    version = '1.0'
}

apply plugin: "org.sonarqube"

subprojects {

    apply plugin: 'java'
    tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    ext.configDir = new File(rootDir, 'config')
    apply from: "$rootDir/gradle/checkstyle.gradle"
    apply from: "$rootDir/gradle/pmd.gradle"
    apply from: "$rootDir/gradle/findbugs.gradle"
    apply from: "$rootDir/gradle/jdepend.gradle"
    apply from: "$rootDir/gradle/jacoco.gradle"

    sonarqube {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.jacoco.reportPath", "build/jacoco/jacocoTest.exec"
        }
    }

    // Disable the test report for the individual test task
    test {
        reports.html.enabled = false
    }


    repositories {
        maven {
            name "gridsum"
            url "http://maven.gridsum.com:8081/nexus/content/groups/public/"
        }
        mavenCentral()
    }

    dependencies {
        compile 'org.apache.commons:commons-lang3:3.3.2'
        compile 'org.apache.ant:ant:1.8.2'

        testCompile group: 'junit', name: 'junit', version: '4.4'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.hamcrest:hamcrest-core:1.3'
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test

}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1"
    }
}